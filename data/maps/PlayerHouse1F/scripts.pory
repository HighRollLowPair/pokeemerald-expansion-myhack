mapscripts PlayerHouse1F_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_FAREWARD_INTRO_STATE, 1: Fareward_PlayerHouse1F_EventScript_MomBeckons
        VAR_FAREWARD_INTRO_STATE, 4: Fareward_PlayerHouse1F_EventScript_MapleConvo
    ]
}

script Fareward_PlayerHouse1F_EventScript_ParentsRoom {
    lockall
    msgbox(Fareward_PlayersHouse1F_Text_ParentsRoom)
    releaseall
}

script Fareward_PlayerHouse1F_EventScript_ArtPiece {
    lockall
    msgbox(Fareward_PlayerHouse1F_Text_ArtPiece)
    releaseall
}

script Fareward_PlayerHouse1F_EventScript_SmallBookshelf {
    lockall
    msgbox(Fareward_PlayerHouse1F_Text_SmallBookshelf)
    releaseall
}

script Fareward_PlayerHouse1F_EventScript_Bookshelf {
    lockall
    msgbox(Fareward_PlayerHouse1F_Text_Bookshelf)
    releaseall
}

script Fareward_PlayerHouse1F_EventScript_MomBeckons {
    lockall
    msgbox(Fareward_PlayerHouse1F_Text_MomBeckons1)
    closemessage
    playse(SE_PIN)
    applymovement(OBJ_EVENT_ID_PLAYER, Fareward_PlayerHouse1F_Movement_MomBeckons1)
    waitmovement(0)
    delay(48)
    msgbox(Fareward_PlayerHouse1F_Text_MomBeckons2)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Fareward_PlayerHouse1F_Movement_MomBeckons2)
    waitmovement(0)
    applymovement(1, Common_Movement_FaceRight)
    waitmovement(0)
    msgbox(Fareward_PlayerHouse1F_Text_MomBeckons3)
    setvar(VAR_FAREWARD_INTRO_STATE, 2)
    releaseall
}

script Fareward_PlayerHouse1F_EventScript_MapleConvo {
    lockall
    playse(SE_PIN)
    applymovement(5, Fareward_PlayerHouse1F_Movement_MapleConvo)
    waitmovement(0)
    delay(4)
    applymovement(4, Common_Movement_FaceDown)
    applymovement(3, Common_Movement_FaceDown)
    waitmovement(4)
    waitmovement(3)
    msgbox(Fareward_PlayerHouse1F_Text_MapleConvo)
    setvar(VAR_FAREWARD_INTRO_STATE, 5)
    clearflag(FLAG_HIDE_CHOOSE_STARTER)
    clearflag(FLAG_HIDE_MAPLES_BAG)
    warp(MAP_FAREWARD, 13, 16)
    waitstate
    releaseall
}

script Fareward_PlayerHouse1F_EventScript_MomSeeNeighbors {
    msgbox(Fareward_PlayerHouse1F_Text_MomSeeNeighbors, MSGBOX_NPC)
}

script Fareward_PlayerHouse1F_EventScript_Dad {
    if (var(VAR_FAREWARD_INTRO_STATE) == 2) {
        msgbox(Fareward_PlayerHouse1F_Text_Dad1, MSGBOX_NPC)
    }
    else {
        msgbox(Fareward_PlayerHouse1F_Text_Dad2, MSGBOX_NPC)
    }
}

script Fareward_PlayerHouse1F_EventScript_Mom {
    msgbox(Fareward_PlayerHouse1F_Text_Mom1, MSGBOX_NPC)
    call(Common_EventScript_OutOfCenterPartyHeal)
    incrementgamestat(GAME_STAT_RESTED_AT_HOME)
    msgbox(Fareward_PlayerHouse1F_Text_Mom2, MSGBOX_NPC)
}

text Fareward_PlayersHouse1F_Text_ParentsRoom {
    "The door to Mom and Dad's room.\n"
    "I shouldn't go in without asking."
}

text Fareward_PlayerHouse1F_Text_ArtPiece {
    "A painting made by a famous artist\n"
    "from the Kalos region.\p"
    "I have trouble pronouncing her name."
}

text Fareward_PlayerHouse1F_Text_SmallBookshelf {
    "The bookshelf is neat and organized,\n"
    "just the way Dad likes it."
}

text Fareward_PlayerHouse1F_Text_Bookshelf {
    "There's mostly books on rare pokémon\n"
    "species and battle strategies,\l"
    "with a couple of sci-fi and\l"
    "romance novels mixed in."
}

text Fareward_PlayerHouse1F_Text_MomBeckons1 {
    "{COLOR BLUE}{SHADOW LIGHT_BLUE}Mom:{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} {PLAYER}!"
}

text Fareward_PlayerHouse1F_Text_MomBeckons2 {
    "{COLOR BLUE}{SHADOW LIGHT_BLUE}Mom:{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} Could you come here? I've got some\n"
    "exciting news to share!"
}

text Fareward_PlayerHouse1F_Text_MomBeckons3 {
    "{COLOR BLUE}{SHADOW LIGHT_BLUE}Mom:{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} Hi, sweetie. Are you feeling ready\n"
    "for the big day today?\p"
    "You must be so excited to receive\n"
    "your first pokémon!\p"
    "When I got my first partner pokémon,\n"
    "I was so excited I could hardly sleep\l"
    "the night before! Ah, that was quite\l"
    "some time ago now...\p"
    "Oh, right, the news. We have some new\n"
    "neighbors moving in today!\p"
    "They're a couple with a daughter\n"
    "named Haley who's about the same\l"
    "age as you.\p"
    "They're moving into the house at the\n"
    "east end of the street. You should\l"
    "stop by and say hello!"
}

text Fareward_PlayerHouse1F_Text_MapleConvo {
    "{COLOR BLUE}{SHADOW LIGHT_BLUE}Prof. Maple:{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} Ah, hello there {PLAYER}!\n"
    "It's good to see you again!\p"
    "I arrived just a few minutes ago.\n"
    "Your mother Grisha tells me that\l"
    "David Jarrett and his family have\l"
    "just begun moving in. That's great\l"
    "to hear; I'm looking forward to\l"
    "working with him.\p"
    "Anyway! Why don't we go see if\n"
    "your friends are ready to choose\l"
    "their first pokémon!"
}

text Fareward_PlayerHouse1F_Text_MomSeeNeighbors {
    "{COLOR BLUE}{SHADOW LIGHT_BLUE}Mom:{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} Have you met the new neighbors yet?\n"
    "They're moving into the house at\l"
    "the east end of the street."
}

text Fareward_PlayerHouse1F_Text_Mom1 {
    "{COLOR BLUE}{SHADOW LIGHT_BLUE}Mom:{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} Hey there, sweetie. Feeling tired?\n"
    "How about you rest for a while."
}

text Fareward_PlayerHouse1F_Text_Mom2 {
    "{COLOR BLUE}{SHADOW LIGHT_BLUE}Mom:{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} Good luck out there!"
}

text Fareward_PlayerHouse1F_Text_Dad1 {
    "{COLOR BLUE}{SHADOW LIGHT_BLUE}Dad:{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} Hey, kiddo! How've you and Damon\n"
    "been getting along?\p"
    "Tell his dad I'd love to go have drinks\n"
    "with him sometime soon, alright?"
}

text Fareward_PlayerHouse1F_Text_Dad2 {
    "{COLOR BLUE}{SHADOW LIGHT_BLUE}Dad:{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} Hey, kiddo! I hope you're having\n"
    "plenty of amazing experiences on your\l"
    "journey out there."
}

movement Fareward_PlayerHouse1F_Movement_MomBeckons1 {
    face_right
    emote_exclamation_mark
}

movement Fareward_PlayerHouse1F_Movement_MomBeckons2 {
    walk_right * 13
    walk_up * 7
    face_left
}

movement Fareward_PlayerHouse1F_Movement_MapleConvo {
    face_down
    emote_exclamation_mark
}
