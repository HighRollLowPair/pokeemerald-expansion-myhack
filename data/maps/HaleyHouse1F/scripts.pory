mapscripts HaleyHouse1F_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_FAREWARD_INTRO_STATE, 2: HaleyHouse1F_EventScript_MomIntroduction
    ]
}

script HaleyHouse1F_EventScript_Cabinet {
    lockall
    msgbox(HaleyHouse1F_Text_Cabinet)
    releaseall
}

script HaleyHouse1F_EventScript_ParentsRoom {
    lockall
    msgbox(HaleyHouse1F_Text_ParentsRoom)
    releaseall
}

script HaleyHouse1F_EventScript_Clock {
    lockall
    incrementgamestat(GAME_STAT_CHECKED_CLOCK)
    fadescreen(FADE_TO_BLACK)
    special(Special_ViewWallClock)
    waitstate
    releaseall
}

script HaleyHouse1F_EventScript_MomIntroduction {
    lockall
    addobject(3)
    playse(SE_PIN)
    applymovement(1, HaleyHouse1F_Movement_MomIntroduction1)
    waitmovement(0)
    msgbox(HaleyHouse1F_Text_MomIntroduction1)
    closemessage
    applymovement(1, Common_Movement_FaceLeft)
    waitmovement(0)
    msgbox(HaleyHouse1F_Text_MomIntroduction2)
    closemessage
    delay(10)
    playse(SE_EXIT)
    delay(80)
    playbgm(MUS_ENCOUNTER_MAY, TRUE)
    applymovement(3, HaleyHouse1F_Movement_MomIntroduction2)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
    delay(80)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
    waitmovement(3)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    applymovement(1, Common_Movement_FaceDown)
    waitmovement(0)
    msgbox(HaleyHouse1F_Text_MomIntroduction3)
    closemessage
    applymovement(1, Common_Movement_FaceLeft)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
    waitmovement(1)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    applymovement(3, HaleyHouse1F_Movement_MomIntroduction3)
    waitmovement(0)
    removeobject(3)
    savebgm(MUS_DUMMY)
    fadedefaultbgm
    applymovement(1, Common_Movement_FaceDown)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
    waitmovement(1)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(HaleyHouse1F_Text_MomIntroduction4)
    closemessage
    applymovement(1, HaleyHouse1F_Movement_MomIntroduction4)
    waitmovement(0)
    setvar(VAR_FAREWARD_INTRO_STATE, 3)
    releaseall
}

script HaleyHouse1F_EventScript_Mom1 {
    lockall
    msgbox(HaleyHouse1F_Text_Mom1, MSGBOX_NPC)
    releaseall
}

script HaleyHouse1F_EventScript_Mom2 {
    lockall
    msgbox(HaleyHouse1F_Text_Mom2, MSGBOX_NPC)
    releaseall
}

script HaleyHouse1F_EventScript_Dad {
    lockall
    msgbox(HaleyHouse1F_Text_Dad, MSGBOX_NPC)
    releaseall
}

text HaleyHouse1F_Text_Cabinet {
    "The cabinet is filled with numerous\n"
    "pokémon-related academic reports,\l"
    "all neatly filed in alphabetical order."
}

text HaleyHouse1F_Text_ParentsRoom {
    "I probably shouldn't go into\n"
    "Haley's parent's room without permission."
}

text HaleyHouse1F_Text_MomIntroduction1 {
    "You must be {PLAYER} from down the street.\n"
    "My name is Margot. It's lovely\l"
    "to meet you!\p"
    "{COLOR BLUE}{SHADOW LIGHT_BLUE}Margot:{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} Your mother and father came by\n"
    "earlier to introduce themselves.\p"
    "I'm sure they've already told you about\n"
    "our daughter Haley.\l"
    "She's upstairs unpacking her belongings."
}

text HaleyHouse1F_Text_MomIntroduction2 {
    "{COLOR BLUE}{SHADOW LIGHT_BLUE}Margot:{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} Haley! Could you come down here\n"
    "and introduce yourself, please?"
}

text HaleyHouse1F_Text_MomIntroduction3 {
    "{COLOR BLUE}{SHADOW LIGHT_BLUE}Haley:{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} You're {PLAYER}, right?\n"
    "Nice to meet you!\p"
    "Your mom said Professor Maple is visiting\n"
    "today to give you and your friend\l"
    "your first pokémon.\p"
    "We moved to the Xemek region\n"
    "because my dad got a job working for\l"
    "the professor, so I'll be getting a\l"
    "partner pokémon from her too.\p"
    "I'm not very interested in becoming\n"
    "a great pokémon master or anything,\l"
    "but at least I'll have something to\l"
    "keep me company in this new region.\p"
    "So I guess I'll see you when we get\n"
    "our pokémon later. For now, I'm gonna\l"
    "go finish unpacking my things.\p"
    "See you around!"
}

text HaleyHouse1F_Text_MomIntroduction4 {
    "{COLOR BLUE}{SHADOW LIGHT_BLUE}Margot:{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} Oh, how exciting it must be to\n"
    "receive your very own pokémon!\p"
    "I do hope you and Haley can become\n"
    "close friends over the course of\l"
    "your journeys.\p"
    "It's been wonderful to meet you, {PLAYER}.\n"
    "Tell your parents that we'd love to\l"
    "have them over for dinner sometime."
}

text HaleyHouse1F_Text_Mom1 {
    "{COLOR BLUE}{SHADOW LIGHT_BLUE}Margot:{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} I know Haley doesn't seem very\n"
    "enthusiastic about receiving\l"
    "her first pokémon, but I'm sure that\l"
    "deep down, she's actually\l"
    "looking forward to it."
}

text HaleyHouse1F_Text_Mom2 {
    "{COLOR BLUE}{SHADOW LIGHT_BLUE}Margot:{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} Hello there, {PLAYER}!\n"
    "I hope you and Haley are\l"
    "getting along well.\p"
    "Where's David? He's at Professor\n"
    "Maple's lab in Grandelia.\p"
    "I'm sure he's doing lots of\n"
    "great work there!"
}

text HaleyHouse1F_Text_Dad {
    "{COLOR BLUE}{SHADOW LIGHT_BLUE}David:{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} It's good to meet you, {PLAYER}.\n"
    "My name is David.\p"
    "As you heard from my daughter,\n"
    "we've moved to Xemek for my new job\l"
    "working as a research assistant\l"
    "for Professor Maple.\p"
    "I look forward to seeing the progress\n"
    "you and Haley make on your\l"
    "pokémon journeys."
}

movement HaleyHouse1F_Movement_MomIntroduction1 {
    face_down
    emote_exclamation_mark
    delay_16 * 3
    walk_down
    walk_left * 3
    face_down
}

movement HaleyHouse1F_Movement_MomIntroduction2 {
    walk_right * 7
    face_down
}

movement HaleyHouse1F_Movement_MomIntroduction3 {
    walk_left * 7
}

movement HaleyHouse1F_Movement_MomIntroduction4 {
    walk_right * 3
    walk_up
    face_right
}